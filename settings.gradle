buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "org.kordamp.gradle:java-project-gradle-plugin:$kordampPluginVersion"
        classpath "org.kordamp.gradle:jacoco-gradle-plugin:$kordampPluginVersion"
        classpath "org.kordamp.gradle:kordamp-parentbuild:$kordampBuildVersion"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$bootPluginVersion"
        classpath "io.spring.gradle:dependency-management-plugin:$dependencyPluginVersion"
    }
}

apply plugin: 'org.kordamp.gradle.kordamp-parentbuild'

rootProject.name = 'redis-cloud-autoscaler'

projects {
    directories = ['autoscaler']

    plugins {
        path(':') {
            id 'org.kordamp.gradle.java-project'
            id 'eclipse'
        }
        dirs(['autoscaler']) {
            id 'java'
            id 'org.springframework.boot'
            id 'io.spring.dependency-management'
        }
        dirs(['autoscaler']){
            id 'application'
        }
    }
}

enforce {
    rule(enforcer.rules.RequireJavaVersion) { r ->
        r.version.set('17')
    }
    rule(enforcer.rules.EnforceBytecodeVersion) { r ->
        // search only on compile and runtime classpaths
        r.enabled = false
    }
    rule(enforcer.rules.BanDuplicateClasses) { r ->
        // search only on compile and runtime classpaths
        r.configurations.addAll(['compileClasspath', 'runtimeClasspath'])
    }
    rule(enforcer.rules.ForceDependencies) { r ->
        r.dependencies.addAll "org.jetbrains:annotations:17.0.0"
    }
    rule(enforcer.rules.DependencyConvergence) { r ->
        r.enabled = false
    }
}